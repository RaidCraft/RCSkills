buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id 'idea'
    id 'io.freefair.lombok' version '5.3.0'
    id 'io.ebean' version '12.5.1'
    id 'kr.entree.spigradle' version '2.2.3'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/publish.gradle"

sourceCompatibility = 11
targetCompatibility = 11

ext {
    mcVersion = project.property("mcVersion")
}

spigot {
    name = project.property("pluginName")
    authors = [project.property("author")]
    apiVersion = project.property("apiVersion")
    load = STARTUP
    //    depends = ['']
}

archivesBaseName = project.property("pluginName")

group project.property("group")

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url = 'https://jitpack.io' }
    spigot()
}

dependencies {
    compileOnly spigot(mcVersion)

    implementation 'net.silthus:ebean-wrapper:2.6.0'
    implementation 'net.silthus.configlib:configlib-bukkit:2.6.0'

    testImplementation 'io.ebean:ebean-test:12.5.1'

    testImplementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    testImplementation group: 'com.h2database', name: 'h2', version: '1.4.200'
    testImplementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.7.0'
    testImplementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.22'
    testImplementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.32.3.2'
    testImplementation group: 'org.postgresql', name: 'postgresql', version: '42.2.18'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.+'
    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation 'org.assertj:assertj-core:3.+'
    testImplementation mockBukkit()
}

shadowJar {
    classifier = ''
    dependencies {
        include(dependency('net.silthus:configlib-bukkit:'))
    }
    relocate 'de.exlll.configlib', "shadow.configlib"
}

tasks.build.dependsOn(shadowJar)
tasks.publish.dependsOn(shadowJar)

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    ignoreFailures = false
}

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

defaultTasks 'build'